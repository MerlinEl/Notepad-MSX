fn generateRoadShortcut = (
		
		--Compatibility Check
		if (selection as Array).count > 1 do return false		--not applicable if two dummies is selected (this is for road creation)
		local road = selection[1]
		if isRoad road do return false 		--not applicable for Road
		if isCrossRoad road do return false	--not applicable for CrossRoad
		--Define Variables
		local road_thickness = 200
		local min_tangent_dist = 5
		local max_tangent_dist = 1000
		local pts = #(), sp = undefined, finalPos = [0,0,0], dp1, dp2, tp1, tp2 -- dp > dummy(point) tp > tangent(point)
		--Check if Shortcut or geometry is selected
		if isShortcut road then ( --recreate current road
		
			if (road.children.count != 3) do ( 
				
				format "roGenStruct > generateRoadShortcut > Invalid components, use Rad Anatomy to see issues\n" road.name
				return false
			)
			--collect components
			sp  = road.children[3]
			dp1 = road.children[1]
			dp2 = road.children[2]
			tp1 = dp1.children[1]
			tp2 = dp2.children[1]
			pts = #(dp1, dp2)
	
			local p1 = dp1.pos
			local p2 = dp2.pos
			local v1 = normalize( tp1.pos - p1 )
			local v2 = normalize( tp2.pos - p2 )
			tp1.pos = p1 + (v1*min_tangent_dist) --move tangents a little further
			tp2.pos = p2 + (v2*min_tangent_dist) --move tangents a little further
			
			--point pos:(p1 + (v1*min_tangent_dist)) wirecolor:red
			--point pos:(p2 + (v2*min_tangent_dist)) wirecolor:green

			finalPos = road.pos
			-- delete old road mesh and spline
			delete sp
			delete road --with mesh delete also remove old custom attribute 
			
		) else if subobjectlevel == 4 do ( --face(s) is selected (build at place)
		
			finalPos = roGenStruct.getSelectedFacesCenter road --try to get selected faces position
			subobjectlevel = 0 
			max select none
		)
		--create new controll points
		if pts.count != 2 do pts = setUpCrossRoadPoints -road_thickness road_thickness 500 hidden_tn:false
		--reverse tangents to inner
		swapPoints pts[1] pts[2]
		--return OK
		SLECT_OBJECT_CALLBACK_ENABLED = false --disable callback before selection
		select pts
		--generate road geometry
		RoadGeneratorTool.btn_generate.pressed() --generate road
		SLECT_OBJECT_CALLBACK_ENABLED = true --enable callback
		--collect components
		road = selection[1]
		sp  = road.RoadGenCa.roadSpline
		dp1 = road.startPoint
		dp2 = road.endPoint
		tp1 = dp1.children[1]
		tp2 = dp2.children[1]
		-------------------------------
		--convert road in to cross road
		-------------------------------
		swapPoints pts[1] pts[2] --reverse tangents outside
		--move tangent point far
		local p1 = dp1.pos
		local p2 = dp2.pos
		local v1 = normalize( tp1.pos - p1 )
		local v2 = normalize( tp2.pos - p2 )
		tp1.pos = p1 + (v1*max_tangent_dist) --move tangents a little further
		tp2.pos = p2 + (v2*max_tangent_dist) --move tangents a little further
		--remove custom attribute
		removeCustomAttribute road #RoadGenCa
		setShortcutCA road sp dp1 dp2 RoadGeneratorTool RoadGeneratorTool.controlsArr
		road.pos = finalPos
	)